plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'maven-publish'
    id 'eclipse'
}

repositories {
    jcenter()
}

version = '0.2.0-SNAPSHOT'
group = 'com.github.sahara3'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    compileOnly 'org.apache.tomcat.embed:tomcat-embed-core:7.0.59'
}

gradlePlugin {
    plugins {
        tomcatRunnerPlugin {
            id = 'com.github.sahara3.tomcat-runner'
            implementationClass = 'com.github.sahara3.gradle.tomcat.TomcatRunnerPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/sahara3/gradle-tomcat-runner-plugin'
    vcsUrl = 'https://github.com/sahara3/gradle-tomcat-runner-plugin.git'
    description = 'Gradle plugin for running Apache Tomcat'
    tags = ['tomcat']

    plugins {
        tomcatRunnerPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Gradle Tomcat Runner plugin'
        }
    }
}

if (version.endsWith('-SNAPSHOT')) {
    publishing {
        publications {
            pluginPublication(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name = 'local'
                url = "file://${rootProject.buildDir}/maven"
            }
        }
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'maven-publish'
    id 'eclipse'
}

apply from: 'version.gradle'

repositories {
    jcenter()
}

version = pluginVersion
group = 'com.github.sahara3'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    compileOnly 'org.apache.tomcat.embed:tomcat-embed-core:7.0.59'

    implementation 'org.apache.commons:commons-lang3:3.9'

    implementation 'org.slf4j:slf4j-api:1.7.30'
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    runtimeOnly 'org.slf4j:jul-to-slf4j:1.7.30'
}

gradlePlugin {
    plugins {
        tomcatRunnerPlugin {
            id = 'com.github.sahara3.tomcat-runner'
            implementationClass = 'com.github.sahara3.gradle.tomcat.TomcatRunnerPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/sahara3/gradle-tomcat-runner-plugin'
    vcsUrl = 'https://github.com/sahara3/gradle-tomcat-runner-plugin.git'
    description = 'Gradle plugin for running Apache Tomcat'
    tags = ['tomcat']

    plugins {
        tomcatRunnerPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'Gradle Tomcat Runner plugin'
        }
    }
}

if (version.endsWith('-SNAPSHOT')) {
    tasks.publishPlugins.enabled = false
    tasks.publish.enabled = false

    publishing {
        publications {
            pluginPublication(MavenPublication) {
                from components.java
            }
        }
    }
}

tasks.clean.doLast {
    project.delete 'bin'
}
